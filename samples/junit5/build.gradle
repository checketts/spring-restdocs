buildscript {
	ext {
		kotlinVersion = '1.2.61'
		springBootVersion = '2.0.4.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RELEASE'
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}

plugins {
	id "org.asciidoctor.convert" version "1.5.3"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'eclipse'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
	mavenLocal()
	maven { url 'https://repo.spring.io/libs-snapshot' }
	mavenCentral()
	maven { url("https://mymavenrepo.com/repo/plFabfZlZhQO7UjsJKDc/") } //kd4smt DSL (kotlin mockmvc dsl
}

group = 'com.example'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
	kotlinOptions.freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable", "-Xprogressive"]
}
compileTestKotlin {
	kotlinOptions.jvmTarget = "1.8"
	kotlinOptions.freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable", "-Xprogressive"]
}

ext {
	snippetsDir = file('build/generated-snippets')
	junitJupiterVersion = '5.0.0'
}

ext['spring-restdocs.version'] = '2.0.3.BUILD-SNAPSHOT'

dependencies {
	asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor:${project.ext['spring-restdocs.version']}"

	compile 'org.springframework.boot:spring-boot-starter-web'

	testCompile('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'junit', module: 'junit;'
	}
	testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
	testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
	testCompile "cz.petrbalat:kd4smt:0.6.0"
	compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

eclipseJdt.onlyIf { false }
cleanEclipseJdt.onlyIf { false }
